# prompt
autoload -U colors && colors
PS1="%{$fg[green]%}%B%n@%m%{$reset_color%}:%{$fg[blue]%}%C%{$reset_color%} %{$fg[red]%}%Bâ€º%{$reset_color%} "

# enable vi mode
bindkey -v

# Do not wait after hitting escape.
KEYTIMEOUT=0

# use the system clipboard
function copy {
  zle vi-yank
  echo "$CUTBUFFER" | wl-copy -n
}
zle -N copy
bindkey -M vicmd 'y' copy
function paste_before {
  CUTBUFFER=$(wl-paste -n)
  zle vi-put-before
}
zle -N paste_before
bindkey -M vicmd 'P' paste_before
function paste_after {
  CUTBUFFER=$(wl-paste -n)
  zle vi-put-after
}
zle -N paste_after
bindkey -M vicmd 'p' paste_after

# set cursor based on keymap
function zle-line-init {
	print -n -- "\e[5 q"
}
function zle-keymap-select {
	case $KEYMAP in
		main) print -n -- "\e[5 q";;
		vicmd) print -n -- "\e[0 q";;
	esac
}
function zle-line-finish {
	print -n -- "\e[0 q"
}
zle -N zle-line-init
zle -N zle-line-finish
zle -N zle-keymap-select

# set highlight colors
zle_highlight=(region:bg=#333333;paste:bg=none)

# enable binding of ^s
stty -ixon

# C-d to interrupt
stty intr \^d

# C-q to quit
stty quit \^q

# H and L to move to the beginning and end of a line.
bindkey -M vicmd 'H' beginning-of-line
bindkey -M vicmd 'L' end-of-line

# fix space issue with tab completion
ZLE_REMOVE_SUFFIX_CHARS=""

# ctrl f to clear screen
bindkey ^f clear-screen
bindkey -M vicmd ^f clear-screen

# ctrl s to list files
function list_widget() {
	echo
	pwd && exa --long --header --all --group-directories-first
	zle reset-prompt
}
zle -N list_widget
bindkey ^s list_widget
bindkey -M vicmd ^s list_widget

# ctrl g to show git status
function git_widget() {
	echo
	git status --short --branch
	zle reset-prompt
}
zle -N git_widget
bindkey ^g git_widget
bindkey -M vicmd ^g git_widget

# ctrl q to exit
function exit_widget() {
	exit
}
zle -N exit_widget
bindkey ^q exit_widget
bindkey -M vicmd ^q exit_widget

# history
HISTSIZE=10000
SAVEHIST=10000
HISTFILE=~/.zhistory
setopt inc_append_history
setopt share_history
setopt extended_history

# misc
export BROWSER="firefox"
export EDITOR="nvim"
export LESS="-R"
export LESSHISTFILE=/dev/null
export OPENER="xdg-open"
export PGUSER=postgres

# less
[ -f ~/.config/.lesskey ] && lesskey

# lf
[ -f ~/.config/lf/icons ] && source ~/.config/lf/icons

# aliases
alias d='trash'
alias e='nvim'
alias f='lf'
alias g='rg'
alias emoji='~/.config/sway/emoji.sh'
alias http='xh'
alias mail='aerc'
alias open='xdg-open'
alias p='bat'
alias rc='$EDITOR ~/.zshrc'
alias src='source ~/.zshrc'
alias t='fd'
alias tree='exa -T'
alias u='cd ..'

function pmsave() {
	pm -Qe | awk '{print $1}' > ~/.packages
}

function screencapture() {
	wf-recorder -f screencapture_$(date +%s).mp4
}

function screenshot() {
	grim -g "$(slurp)" screenshot_$(date +%s).png
}

function screencolor() {
	grim -g "$(slurp -p)" -t ppm - | convert - -format "%[pixel:p{0,0}]" txt:-
}

function weather() {
	curl -sSL http://wttr.in/$(echo "$@" | tr " " +)
}

function cheat() {
	curl -sSL http://cheat.sh/$(echo "$@" | tr " " +)
}

function csv() {
	column -s, -t < $@ | less -S
}
